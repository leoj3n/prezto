#!/usr/bin/env zsh

################################################################################
# Help
################################################################################

#
# Disallow sudo to keep the space-time continuum intact.
#

(( EUID == 0 )) && print "
${0:t}: An encounter with sudo could create a time paradox.

The results of which could cause a chain reaction that would unravel the
very fabric of the space-time continuum and destroy the entire universe!
... Granted, that's the worst-case scenario. The destruction however
might be limited merely to our own galaxy.
" >&2 && exit 1

#
# Usage, or, What does this button do!?
#

local usage="
usage: ${0:t} [-option ...] [--] command

options:
  -s         System-wide installation (/etc/zshenv instead of ~/.zshenv).

commands:
  forward    Drives the latest version into place.
  reverse    Backs out the changes made by DeLorean.
"

################################################################################
# Setup
################################################################################

#
# Defaults.
#

local zshenv_ext='user'
local zshenv_loc="${HOME}/.zshenv"

#
# Get options.
#

while getopts ':s' opt; do
  case "${opt}" in
    (s)
      zshenv_ext='system'
      zshenv_loc="/etc/zshenv"
      ;;
    (:)
      print "${0:t}: option requires an argument: ${OPTARG}" >&2
      print "${usage}" >&2
      exit 1
      ;;
    ([?])
      print "${0:t}: unknown option: ${OPTARG}" >&2
      print "${usage}" >&2
      exit 1
    ;;
  esac
done
shift $((OPTIND - 1))

if (( $# < 1 )); then
  print "${usage}" >&2
  exit 1
fi

################################################################################
# Run
################################################################################

#
# Attempt temporal displacement of zshenv.
#

if [[ -s "${zshenv_loc}" && ! "$(<"${zshenv_loc}")" =~ 'DeLorean' ]]; then
  if read -q "?Overwrite ${zshenv_loc} [y/N]? "; then
    print #spacing
  else
    print "\n${0:t}: Aborting temporal displacement!\n"
    exit 1
  fi
fi

local zshenv_schema="${"$(<"${0:a:h}/schematic/zshenv.${zshenv_ext}")"//__ZDOTDIR__/"${0:a:h}/ZDOTDIR"}"
print "${zshenv_schema}" | $([[ "$zshenv_ext" == 'system' ]] && print 'sudo') tee "${zshenv_loc}" >/dev/null

#
# Critical velocity has been reached (88 MPH)!
#

[[ "$(<"${0:a:h}/README.md")" =~ '```DeLorean(.*)```' ]] && print -f "%s\n" "${match[1]}"

if (( $+commands[afplay] )); then
  afplay "${0:a:h}/.github/bttf.wav" 2>&1 >/dev/null &
elif (( $+commands[aplay] )); then
  aplay -t wav "${0:a:h}/.github/bttf.wav" 2>&1 >/dev/null &
fi

NOFORTUNE=1 JIGOWATTS=1.21 exec "$SHELL" -l
